plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'dev.keiji.apdu'
version '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "${group}"
            artifactId = 'apdu'
            version = "${version}"

            from components.java

                    pom {
                        name = 'APDU for Java'
                        description = 'Utilities for encoding the command of APDU and decoding the response representation of binary data.'
                        url = 'https://github.com/keiji/javacard-practice/tree/main/android-app/apdu'
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        developers {
                            developer {
                                id = 'keiji'
                                name = 'ARIYAMA Keiji'
                                email = 'keiji.ariyama@gmail.com'
                            }
                        }
                        scm {
                            connection = 'https://github.com/keiji/javacard-practice.git'
                            developerConnection = 'https://github.com/keiji/javacard-practice.git'
                            url = 'https://github.com/keiji/javacard-practice/tree/main/android-app/apdu'
                        }
                    }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('-SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            def sonatypeUsername = project.getProperties().get('sonatypeUsername') ?: ""
            def sonatypePassword = project.getProperties().get('sonatypePassword') ?: ""
            credentials {
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
    }
}

signing {
    if (!project.getProperties().get('signing.password')) {
        System.console().printf("signing.password must be set.")
        return
    }
    sign publishing.publications.mavenJava
}
